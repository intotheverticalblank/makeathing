 set zoneheight 16
 displaymode 160A
 set plotvalueonscreen on
 set romsize 128kRAM
 set dlmemory $4000 $7FFF
 doublebuffer on 

 incgraphic font.png 160A 
 characterset font
 alphachars '0123456789abcdefghijklmnopqrstuvwxyz>`?!/.,-_()[]&AB '

 rem sprites
 incgraphic images/player1.png 160A 0 1 3 2
 incgraphic images/player2.png 160A 0 1 3 2
 incgraphic images/player3.png 160A 0 1 3 2
 incgraphic images/player4.png 160A 0 1 3 2

 incgraphic images/orb1.png 160A 0 3 2 1
 incgraphic images/orb2.png 160A 0 3 2 1
 incgraphic images/orb3.png 160A 0 3 2 1 
 incgraphic images/orb4.png 160A 0 3 2 1
 incgraphic images/orb5.png 160A 0 3 2 1 
 incgraphic images/orb6.png 160A 0 3 2 1 
 
 incbanner  images/title.png 160B 

 BACKGRND=$00

 

 dim bgColor  = var1
 dim wait = var2
 dim lives = var3
 rem dim score = var4
 dim playerX =var5
 dim playerY = var6
 dim playerAnimFrame = var7
 dim playerAnimWait = var8

 dim orbX =var9
 dim orbY = var10
 dim orbAnimFrame = var11
 dim orbAnimWait = var12

 dim randomSeed = var13


 bgColor = $00
 wait = 0
 lives = 0
 score0 = 0
 playerY = 0
 playerX = 0
 playerAnimFrame = 0
 playerAnimWait = 0

 orbX = 0
 orbY = 0
 orbAnimFrame = 0
 orbAnimWait = 0

 randomSeed = 1



 BACKGRND = $00

_setTitleColors
 P0C1 = title_color1
 P0C2 = title_color2
 P0C3 = title_color3
 P1C1 = title_color4
 P1C2 = title_color5
 P1C3 = title_color6
 P2C1 = title_color7
 P2C2 = title_color8
 P2C3 = title_color9
 P3C1 = title_color10
 P3C2 = title_color11
 P3C3 = title_color12


_titleLoop
 clearscreen
 plotbanner title 0 32 50

 plotchars 'quantum lay over'  3 47 6
 plotchars 'fire to start'  2 54 10
 if joy0fire0  || joy0fire1 then goto _initGame
 
 randomSeed = randomSeed + 1
 if randomSeed > 254  then randomSeed = 1
 
 drawscreen

 goto _titleLoop

 
_initGame
 
 lives = 3
 score0 = 0
 BACKGRND=$00
 playerX = 70
 playerY = 90
 playerAnimFrame = 0
 playerAnimWait = 0

 rand = randomSeed
 
 gosub _randomOrb

 orbAnimFrame = 0
 orbAnimWait = 0

 rem pallette 0 text
  P0C1=$0F
  P0C2=$0C
  P0C3=$0A

 rem pallette 1 player
  P1C1 = $0D 
  P1C2 = $08
  P1C3 = $33 

 rem orb colors

  P2C1 = $92
  P2C2 = $94
  P2C3 = $99


_gameLoop

 clearscreen

 plotsprite orb1 2 orbX orbY orbAnimFrame
 orbAnimWait = orbAnimWait +1
 if orbAnimWait > 1 then  orbAnimFrame = orbAnimFrame + 1 : orbAnimWait = 0
 if orbAnimFrame > 5 then orbAnimFrame = 0


 plotsprite player1 1 playerX playerY playerAnimFrame
 playerAnimWait = playerAnimWait +1
 if playerAnimWait > 2 then  playerAnimFrame = playerAnimFrame + 1 : playerAnimWait = 0
 if playerAnimFrame > 3 then playerAnimFrame = 0

 if joy0left  then playerX=playerX - 1
 if joy0right then playerX=playerX +  1
 if joy0up  then playerY=playerY - 1
 if joy0down then playerY=playerY +  1

 gosub _testCollisions

 plotvalue font 1 score0 6 124 0

 doublebuffer flip 2
 goto _gameLoop

_testCollisions
 if boxcollision(playerX, playerY, 16, 16, orbX, orbY, 8, 8) then goto _doCollide
 goto _skipCollide 
_doCollide
 score0 = score0 + 10
 gosub _randomOrb
_skipCollide

 return

_randomOrb
 orbX = rand  
 orbY = rand
 if orbX > 151 then orbX = 151
 if orbY > 183 then orbY = 183  
 return

